import groovy.json.JsonSlurper

apply plugin: "java-library"
apply plugin: "kotlin"

dependencies {
    //noinspection DifferentStdlibGradleVersion
    implementation(Libs.kotlin_stdlib_jdk8)
    implementation(project(":annotations"))
    implementation(Libs.kotlinpoet)
    implementation(Libs.kotlinx_metadata_jvm)
    implementation(Libs.kotlinx_serialization_runtime)
}

buildscript {

    def versionFile = file("../../../lerna.json")
    def parsedVersion = new JsonSlurper().parseText(versionFile.text)
    project.version = parsedVersion.version.toString()

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(Libs.kotlin_gradle_plugin)
    }
}

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

apply from: rootProject.file("gradle/lint.gradle")
apply from: rootProject.file("gradle/publishing.gradle")
