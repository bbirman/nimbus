buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath(Libs.com_android_tools_build_gradle)
        classpath(Libs.kotlin_gradle_plugin)
        classpath(Libs.gradle_bintray_plugin)
        classpath(Libs.android_maven_gradle_plugin)
        classpath(Libs.dokka_gradle_plugin)
        classpath(Libs.jacoco_android)
        classpath(Libs.org_jetbrains_kotlin_kotlin_serialization)
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:4.15.2")

    }
}

plugins{
    id("de.fayard.buildSrcVersions") version "0.7.0"
}
project.ext {
    if (project.hasProperty("preDexEnable")) {
        project.ext.preDexLibs = project.properties["preDexEnable"] == "true"
    } else {
        project.ext.preDexLibs = true // pre dexing should be true by default
    }

    println("PREDEX " + (project.ext.preDexLibs ? "ENABLED" : "DISABLED")) // goes to build log or console output
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://dl.bintray.com/salesforce-mobile/android"
        }
    }
    group GROUP
    version VERSION
}

subprojects {
    task printPubs {
        doLast {
            if (project.hasProperty("publishing")) {
                project.publishing.publications.each { publication ->
                    println "Publication [name: $publication.name, groupId: $publication.groupId, artifactId: $publication.artifactId, version: $publication.version"
                }
            } else {
                println("No publishing for project ${project.name}")
            }
        }
    }

    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
// TODO: Move lint to plugin
// TODO: Move Dokka config to plugin
